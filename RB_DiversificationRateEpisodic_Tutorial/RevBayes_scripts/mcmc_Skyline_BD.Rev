################################################################################
#
# RevBayes Example: Bayesian inference of diversification rates under a
#                   skyline birth-death model
# 
#
# authors: Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

T <- readTrees("data/primates_Springer.tre")[1]

# Get some useful variables from the data. We need these later on.
taxa <- T.taxa()

# set my move index
mi = 0

NUM_INTERVALS = 10

######################
# birth-death model  #
######################



####################
# Create the rates #
####################


for (i in 1:NUM_INTERVALS) {
    
    speciation[i] ~ dnUniform( 0, 10.0 )
    extinction[i] ~ dnUniform( 0, 10.0  )


    moves[++mi] = mvScale(speciation[i], weight=2)
    moves[++mi] = mvScale(extinction[i], weight=2)

}

moves[++mi] = mvVectorScale(speciation, weight=10)
moves[++mi] = mvVectorScale(extinction, weight=10)


interval_times = T.rootAge() * (1:(NUM_INTERVALS-1)) / (NUM_INTERVALS) * 0.8


### rho is the probability of sampling species at the present
### fix this to 369/450, since there are ~450 described species of primates
### and we have sampled 369
rho <- 369/450

timetree ~ dnEpisodicBirthDeath(rootAge=T.rootAge(), lambdaRates=speciation, lambdaTimes=interval_times, muRates=extinction, muTimes=interval_times, rho=rho, taxa=taxa)

### clamp the model with the "observed" tree
timetree.clamp(T)



#############
# The Model #
#############


### workspace model wrapper ###
mymodel = model(rho)

### set up the monitors that will output parameter values to file and screen 
monitors[1] = mnModel(filename="output/primates_skyline_BD.log",printgen=10, separator = TAB)
monitors[2] = mnFile(filename="output/primates_skyline_BD_speciation.log",printgen=10, separator = TAB, speciation)
monitors[3] = mnFile(filename="output/primates_skyline_BD_extinction.log",printgen=10, separator = TAB, extinction)
monitors[4] = mnScreen(printgen=1000, speciation)



################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves)

### pre-burnin to tune the proposals ###
mymcmc.burnin(generations=10000,tuningInterval=200)

### run the MCMC ###
mymcmc.run(generations=50000)



## quit ##
q()