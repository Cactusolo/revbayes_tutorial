################################################################################
#
# RevBayes Example: Bayesian inference of diversification rates under a
#                   branch-specific birth-death model
# 
#
# authors: Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

T <- readTrees("data/primates_Springer.tre")[1]

# Get some useful variables from the data. We need these later on.
taxa <- T.taxa()

# set my move index
mi = 0


NUM_RATE_CATEGORIES = 10



####################
# Create the rates #
####################

# Specify a prior on the diversification and turnover rate
mean_speciation ~ dnUniform(-10,10)
sd_speciation <- 0.587405*2
speciation := fnDiscretizeDistribution( dnLognormal(mean_speciation, sd_speciation), NUM_RATE_CATEGORIES )
moves[++mi] = mvSlide(mean_speciation,delta=1,tune=true,weight=5)


### Create a random variable for the relative extinction rate
relative_extinction ~ dnBeta(1,1)
moves[++mi] = mvSlide(relative_extinction,delta=1,tune=true,weight=5)
### Compute the extinction rate by multiplying the speciation rate with the relative extinction rate
for (i in 1:NUM_RATE_CATEGORIES) {
    extinction[i] := relative_extinction * exp( speciation[i] )
}

### Create a uniformly distributed variable for the category at the root
root_category ~ dnUniformNatural(1,NUM_RATE_CATEGORIES)
moves[++mi] = mvRandomIntegerWalk(root_category,weight=1)


### Create a exponential distributed variable for the rate of rate-shift events
event_rate ~ dnExponential(1.0)
moves[++mi] = mvScale(event_rate,lambda=1,tune=true,weight=5)


### Create the fix parameter for the age of the root set to the observed age
root <- T.rootAge()


### rho is the probability of sampling species at the present
### fix this to 369/450, since there are ~450 described species of primates
### and we have sampled 369
rho <- 369/450


#### construct a variable for the tree drawn from a birth death process
timetree ~ dnHBDP(lambda=speciation, mu=extinction, rootAge=root, rho=rho, rootState=root_category, delta=event_rate, taxa=taxa )

### clamp the model with the "observed" tree
timetree.clamp(T)


### Create moves that change the augmented rate-shift events on the tree
moves[++mi] = mvBirthDeathEvent(timetree,weight=2)
moves[++mi] = mvEventTimeSlide(timetree,weight=2)
moves[++mi] = mvEventTimeBeta(timetree,weight=2)
moves[++mi] = mvDiscreteEventCategoryRandomWalk(timetree,weight=2)

### Create some deterministic variable that hold information about the rates per branch
num_events := timetree.numberEvents()
avg_lambda := timetree.averageSpeciationRate()
avg_mu     := timetree.averageExtinctionRate()

total_num_events := sum( num_events )




#############
# The Model #
#############


### workspace model wrapper ###
mymodel = model(rho)

### set up the monitors that will output parameter values to file and screen 
monitors[1] = mnModel(filename="output/primates_BSBD.log",printgen=10, separator = TAB)
monitors[2] = mnExtNewick(filename="output/primates_BSBD.trees", isNodeParameter=FALSE, printgen=10, separator = TAB, tree=timetree, avg_lambda, avg_mu)
monitors[3] = mnScreen(printgen=10, event_rate, mean_speciation, root_category, total_num_events)


################
# The Analysis #
################

### workspace mcmc
mymcmc = mcmc(mymodel, monitors, moves)

### pre-burnin to tune the proposals
mymcmc.burnin(generations=10000,tuningInterval=200)

### run the MCMC
mymcmc.run(generations=50000)

### read in the output and create a MAP tree with the rates per branch attached to it
treetrace = readTreeTrace("output/primates_BSBD.trees", treetype="clock")
map_tree = mapTree(treetrace,"output/primates_BSBD_MAP.tree")


## quit ##
q()