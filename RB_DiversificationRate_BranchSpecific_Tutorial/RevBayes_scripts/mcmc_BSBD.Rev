################################################################################
#
# RevBayes Example: Bayesian inference of diversification rates under a
#                   branch-specific birth-death model
# 
#
# authors: Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

T <- readTrees("data/primates_Springer.tre")[1]

# Get some useful variables from the data. We need these later on.
taxa <- T.taxa()

# set my move index
mvi = 0


NUM_RATE_CATEGORIES = 10



####################
# Create the rates #
####################

### Specify a prior on the diversification and turnover rate

### Create a lognormal distributed variable for the mean diversification rate
diversification_prior_mean <- ln( ln(450.0/2.0) / T.rootAge() )
diversification_mean ~ dnLognormal(mean=diversification_prior_mean, sd=0.587405)
moves[++mvi] = mvScale(diversification_mean,lambda=1,tune=true,weight=5)


### Create a deterministic variable for the diversification rate categories
### using a discretized lognormal distribution (the N-quantiles of it)
diversification_sd <- 0.587405*2
diversification := fnDiscretizeDistribution( dnLognormal(ln(diversification_mean), diversification_sd), NUM_RATE_CATEGORIES )


### Create a lognormal distributed variable for the turnover rate
turnover_prior_mean <- ln( ln(450.0/2.0) / T.rootAge() )
turnover_mean ~ dnLognormal(mean=turnover_prior_mean,sd=0.587405*2) 
moves[++mvi] = mvScale(turnover_mean,lambda=1.0,tune=true,weight=3.0)

### Create a deterministic variable for the turnover rate categories
### using a discretized lognormal distribution (the N-quantiles of it)
turnover := fnDiscretizeDistribution( dnLognormal(ln(turnover_mean), 0.587405), NUM_RATE_CATEGORIES )


### Transform the parameters
speciation := diversification + turnover
extinction := turnover 


### Create a uniformly distributed variable for the category at the root
root_category ~ dnUniformNatural(1,NUM_RATE_CATEGORIES)
moves[++mvi] = mvRandomIntegerWalk(root_category,weight=1)


### Create a exponential distributed variable for the rate of rate-shift events
event_rate ~ dnExponential(1.0)
moves[++mvi] = mvScale(event_rate,lambda=1,tune=true,weight=5)


### Create the fix parameter for the age of the root set to the observed age
root <- T.rootAge()


### rho is the probability of sampling species at the present
### fix this to 367/450, since there are ~450 described species of primates
### and we have sampled 367
rho <- T.ntips()/450


#### construct a variable for the tree drawn from a birth death process
timetree ~ dnHBDP(lambda=speciation, mu=extinction, rootAge=root, rho=rho, rootState=root_category, delta=event_rate, taxa=taxa )

### clamp the model with the "observed" tree
timetree.clamp(T)


### Create moves that change the augmented rate-shift events on the tree
moves[++mvi] = mvBirthDeathEvent(timetree,weight=2)
moves[++mvi] = mvEventTimeSlide(timetree,weight=2)
moves[++mvi] = mvEventTimeBeta(timetree,weight=2)
moves[++mvi] = mvDiscreteEventCategoryRandomWalk(timetree,weight=2)

### Create some deterministic variable that hold information about the rates per branch
num_events := timetree.numberEvents()
avg_lambda := timetree.averageSpeciationRate()
avg_mu     := timetree.averageExtinctionRate()

total_num_events := sum( num_events )




#############
# The Model #
#############


### workspace model wrapper ###
mymodel = model(rho)

### set up the monitors that will output parameter values to file and screen 
monitors[1] = mnModel(filename="output/primates_BSBD.log",printgen=10, separator = TAB)
monitors[2] = mnExtNewick(filename="output/primates_BSBD.trees", isNodeParameter=FALSE, printgen=10, separator = TAB, tree=timetree, avg_lambda, avg_mu)
monitors[3] = mnScreen(printgen=10, event_rate, diversification_mean, turnover_mean, root_category, total_num_events)


################
# The Analysis #
################

### workspace mcmc
mymcmc = mcmc(mymodel, monitors, moves)

### pre-burnin to tune the proposals
mymcmc.burnin(generations=10000,tuningInterval=200)

### run the MCMC
mymcmc.run(generations=50000)

### read in the output and create a MAP tree with the rates per branch attached to it
treetrace = readTreeTrace("output/primates_BSBD.trees", treetype="clock")
map_tree = mapTree(treetrace,"output/primates_BSBD_MAP.tree")


## quit ##
q()